program = _{ SOI ~ code ~ EOI }

code = _{ NEWLINE* ~ ((import | directImport) ~ ";"? ~ NEWLINE+)* ~ (import | directImport)? ~ ";"? ~ rest }
// Expression(Generic)
directImport = { NEWLINE* ~ "import" ~ source }
import       = { NEWLINE* ~ "import" ~ (defaultWithDestructuring | destructuring | rename | ident) ~ "from" ~ source }

source      = @{ quota | singleQuota | backtick }
quota       =  { "\"" ~ ("\\\"" | (!"\"" ~ ANY))* ~ "\"" }
singleQuota =  { "\'" ~ ("\\\'" | (!"\'" ~ ANY))* ~ "\'" }
backtick    =  { "`" ~ ("\\`" | (!"`" ~ ANY))* ~ "`" }

rename = { pastName ~ "as" ~ NEWLINE* ~ ident }

pastName                 = _{ (ident | "*") }
destructuring            =  {
    "{" ~ NEWLINE* ~ "}"
  | "{" ~ NEWLINE* ~ (rename | ident) ~ ("," ~ (rename | ident))* ~ NEWLINE* ~ "}"
}
defaultWithDestructuring =  { ident ~ "," ~ NEWLINE* ~ destructuring }

ident = @{ ASCII_ALPHA ~ (ASCII_ALPHANUMERIC | "_" | "$" | "@")* }

rest       = @{ ANY* }
WHITESPACE = _{ " " | "\t" }

COMMENT = _{ "//" ~ (!"\n" ~ ANY)* }
